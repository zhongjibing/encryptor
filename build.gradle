plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group = 'com.icezhg'
version = '1.1.0'
description = 'encryptor'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }

    withSourcesJar()
}

dependencies {
    implementation 'org.bouncycastle:bcprov-jdk18on:1.80'
    implementation 'commons-codec:commons-codec:1.18.0'
}

configurations.configureEach() {
    Configuration configuration ->
        configuration.resolutionStrategy.failOnVersionConflict()
}


repositories {
    mavenLocal()
    maven {
        name = 'release'
        url 'https://packages.aliyun.com/maven/repository/2027113-release-yjsA6r/'
        credentials(PasswordCredentials)
    }
    maven {
        name = 'snapshot'
        url 'https://packages.aliyun.com/maven/repository/2027113-snapshot-4NJMQr/'
        credentials(PasswordCredentials)
    }
    maven { url 'https://repo1.maven.org/maven2/' }
    maven { url 'https://maven.aliyun.com/repository/public' }
    mavenCentral()
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
        maven {
            name = 'aliyun'
            def releasesRepoUrl = 'https://packages.aliyun.com/maven/repository/2027113-release-yjsA6r/'
            def snapshotsRepoUrl = 'https://packages.aliyun.com/maven/repository/2027113-snapshot-4NJMQr/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials)
        }
    }
}


